plugins {
    id("application")
    id("java")
    id("com.github.johnrengelman.shadow") version("8.1.1")
    id("io.freefair.lombok") version("8.6")
}

group = "com.sotasan"
version = "0.9.8"

repositories {
    mavenCentral()
    maven { url("https://jitpack.io") }
    maven { url("https://maven.fabricmc.net") }
}

application {
    mainClass = "${project.group}.${project.name.toLowerCase()}.Main"
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

dependencies {
    implementation("com.fifesoft:rsyntaxtextarea:3.4.0")
    implementation("com.formdev:flatlaf:3.4.1")
    implementation("com.formdev:flatlaf-extras:3.4.1")
    implementation("com.formdev:flatlaf-fonts-inter:4.0")
    implementation("com.formdev:flatlaf-fonts-jetbrains-mono:2.304")
    implementation("com.github.java-decompiler:jd-core:v1.1.3")
    implementation("com.miglayout:miglayout-swing:11.3")
    implementation("net.fabricmc:cfr:0.2.2")
    implementation("org.bitbucket.mstrobel:procyon-compilertools:0.6.0")
    implementation("org.jetbrains:annotations:24.1.0")
    implementation("org.vineflower:vineflower:1.10.0")
}

processResources {
    outputs.upToDateWhen { false }
    filesMatching("application.properties") {
        expand(project.properties)
    }
}

jar {
    enabled = false
}

shadowJar {
    dependsOn(distTar, distZip)
    archiveBaseName = project.name.toLowerCase()
    archiveClassifier = null
}

test {
    enabled = false
}

tasks.register("testJar", Jar) {
    archiveFileName = "test.jar"
    from(sourceSets.test.output)
}

run {
    dependsOn(testJar)
    args((testJar as Jar).archiveFile.get())
}